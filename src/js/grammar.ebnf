<谱字> = <简单式> | <复合式> | <旁注式> | <独体式>

<简单式> = <徽位指法短语> <特殊指法>? <弦序指法短语>
<徽位指法短语> = <非散音指法短语> | "散音"
<非散音指法短语> = <徽位指法> <徽分位置>
<特殊指法> = "注" | "绰"
<弦序指法短语> = <弦序指法> <弦序>

<徽位指法> = "大指" | "食指" | "中指" | "名指" | "跪指"
<徽分位置> = <徽数> <分数>?
<徽数> = "十一徽" | "十二徽" | "十三徽" | "一徽" | "二徽" | "三徽" | "四徽" | "五徽" | "六徽" | "七徽" | "八徽" | "九徽" | "十徽" | "徽外"
<分数> = "一分" | "二分" | "三分" | "四分" | "五分" | "六分" | "七分" | "八分" | "九分" | "半"

<弦序指法> = "如一声" | "抹挑" | "勾剔" | "打摘" | "托擘" | "半轮" | "长琐" | "如一" | "滚拂" | "全扶" | "剌伏" | "打圆" | "搯起" | "抓起" | "带起" | "虚掩" | "推出" | "不动" | "擘" | "托" | "抹" | "挑" | "勾" | "剔" | "打" | "摘" | "历" | "蠲" | "轮" | "琐" | "伏" | "滚" | "拂" | "至" | "拨" | "剌" | "掩"
<弦序> = "一弦" | "二弦" | "三弦" | "四弦" | "五弦" | "六弦" | "七弦"

<复合式> = <复式指法> <复子式> <复子式>
<复式指法> = "掐撮" | "双弹" | "拨剌" | "齐撮" | "撮"
<复子式> = <徽位指法短语> <特殊指法>? <弦序指法>? <弦序>. #掐撮大指七徽注勾三弦

<旁注式> = <修饰词> | <走位指法短语> | (<修饰词> <走位指法短语>)
<修饰词> = "引" | "淌" | "急" | "缓" | "紧" | "慢"
<走位指法短语> = <走位指法> <徽分位置>?  
<走位指法> = "进复" | "退复" | "往来" | "上" | "下" | "进" | "退" | "复" | "撞" | "逗" | "唤" | "吟" | "猱"

<独体式> = <记号>
<记号> = "从再作" | "从头再作" | "少息" | "大息" | "入拍" | "入慢" | "句号" | "再作" | "曲终" | "操终" | "泛起" | "泛止" | "间"